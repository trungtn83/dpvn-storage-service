plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'maven-publish'
}

group = 'com.dpvn'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    set('springCloudVersion', "2023.0.1")
}

dependencies {
    implementation('com.dpvn:shared-core:0.0.1-SNAPSHOT')
    implementation('com.dpvn:shared-jpa:0.0.1-SNAPSHOT')

    implementation 'net.coobird:thumbnailator:0.4.14'
    implementation 'org.apache.tika:tika-core:3.2.2'

    implementation("com.mysql:mysql-connector-j:9.2.0")

    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

spotless {
    java {
        target '**/*.java'
        googleJavaFormat("1.22.0")
        importOrder()
        toggleOffOn('/*', '*/')
    }
}

task domainJar(type: Jar) {
    archiveClassifier.set('domain')
    from(sourceSets.main.output) {
        include 'com/dpvn/storageservice/domain/dto/**'
        include 'com/dpvn/storageservice/util/**'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact domainJar
        }
    }
}

bootJar {
    if (project.hasProperty('jarName')) {
        archiveBaseName = project.getProperty('jarName')
        archiveVersion = '' // Set version to empty to exclude version from the JAR name
    }
    manifest {
        attributes(
                'Implementation-Title': archiveBaseName,
                'Implementation-Version': archiveVersion
        )
    }
    mainClass.set('com.dpvn.storageservice.StorageServiceApplication')
}
