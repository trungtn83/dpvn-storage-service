plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'maven-publish'
}

group = 'com.dpvn'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    set('springCloudVersion', "2023.0.1")
}

dependencies {
    implementation('com.dpvn:dpvn-shared:0.0.1-SNAPSHOT')

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'net.coobird:thumbnailator:0.4.14'
    implementation 'org.apache.tika:tika-core:3.2.2'

    implementation("com.mysql:mysql-connector-j:9.2.0")

    implementation 'org.apache.httpcomponents.client5:httpclient5:5.3.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

spotless {
    java {
        target '**/*.java'
        googleJavaFormat("1.22.0")
        importOrder()
        toggleOffOn('/*', '*/')
    }
}

task domainJar(type: Jar) {
    from(sourceSets.main.output) {
        include 'com/dpvn/storageservice/domain/**'
    }

    manifest {
        attributes(
                'Main-Class': 'com.dpvn.storageservice.StorageServiceApplication'
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact domainJar
        }
    }
}

bootJar {
    if (project.hasProperty('jarName')) {
        archiveBaseName = project.getProperty('jarName')
        archiveVersion = '' // Set version to empty to exclude version from the JAR name
    }
    manifest {
        attributes(
                'Implementation-Title': archiveBaseName,
                'Implementation-Version': archiveVersion
        )
    }
}
